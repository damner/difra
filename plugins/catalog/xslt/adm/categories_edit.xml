<?xml version="1.0" encoding="UTF-8"?>
<xsl:stylesheet xmlns:xsl="http://www.w3.org/1999/XSL/Transform" xmlns="http://www.w3.org/1999/xhtml" version="1.0">
	<xsl:template match="CatalogCategoriesAdd">
		<h2>
			<a href="/adm/catalog/categories">
				<xsl:value-of select="$locale/catalog/adm/title-categories"/>
			</a>
			<xsl:text> → </xsl:text>
			<xsl:value-of select="$locale/catalog/adm/title-categories-add"/>
		</h2>
		<h3>
			<xsl:value-of select="$locale/catalog/adm/options"/>
		</h3>
		<xsl:call-template name="CatalogCategoriesForm"/>
	</xsl:template>

	<xsl:template match="CatalogCategoriesEdit">
		<h2>
			<a href="/adm/catalog/categories">
				<xsl:value-of select="$locale/catalog/adm/title-categories"/>
			</a>
			<xsl:text> → </xsl:text>
			<xsl:value-of select="$locale/catalog/adm/title-categories-edit"/>
		</h2>
		<h3>
			<xsl:value-of select="$locale/catalog/adm/options"/>
		</h3>
		<xsl:call-template name="CatalogCategoriesForm"/>
	</xsl:template>

	<xsl:template name="CatalogCategoriesForm">
		<form action="/adm/catalog/categories/save" method="post" class="ajaxer">
			<xsl:if test="@id">
				<input type="hidden" name="id" value="{@id}"/>
			</xsl:if>
			<table class="form">
				<tr>
					<th>
						<xsl:value-of select="$locale/catalog/adm/parent-category"/>
					</th>
					<td>
						<select name="parent" class="catalogCategory">
							<option value="0">
								<xsl:value-of select="$locale/catalog/adm/catalog-root"/>
							</option>
							<xsl:call-template name="CatalogCategorySelect">
								<xsl:with-param name="selected" select="@parent"/>
								<xsl:with-param name="maxdepth" select="@maxdepth"/>
							</xsl:call-template>
						</select>
					</td>
				</tr>
				<tr>
					<th>
						<xsl:value-of select="$locale/catalog/adm/name"/>
					</th>
					<td>
						<input type="text" name="name" value="{@name}"/>
					</td>
				</tr>
			</table>
			<input type="submit" class="large_spacing" value="{$locale/catalog/adm/save}"/>
		</form>
	</xsl:template>

	<xsl:template name="categorySelectSpaces">
		<xsl:param name="num"/>
		<xsl:if test="$num &gt; 0">
			&#160;&#160;&#160;&#160;&#160;
			<xsl:call-template name="categorySelectSpaces">
				<xsl:with-param name="num" select="$num - 1"/>
			</xsl:call-template>
		</xsl:if>
	</xsl:template>

	<xsl:template name="CatalogCategorySelect">
		<xsl:param name="parent" select="0"/>
		<xsl:param name="depth" select="1"/>
		<xsl:param name="node" select="."/>
		<xsl:param name="selected" select="-1"/>
		<xsl:param name="maxdepth" select="0"/>

		<xsl:if test="not($maxdepth>0) or $depth &lt; $maxdepth">
			<xsl:for-each select="$node/category[@parent=$parent]">
				<option value="{@id}">
					<xsl:if test="@id=$selected">
						<xsl:attribute name="selected">selected</xsl:attribute>
					</xsl:if>
					<xsl:call-template name="categorySelectSpaces">
						<xsl:with-param name="num" select="$depth -1"/>
					</xsl:call-template>
					<xsl:value-of select="@name"/>
				</option>
				<xsl:call-template name="CatalogCategorySelect">
					<xsl:with-param name="depth" select="$depth+1"/>
					<xsl:with-param name="parent" select="@id"/>
					<xsl:with-param name="node" select=".."/>
					<xsl:with-param name="selected" select="$selected"/>
					<xsl:with-param name="maxdepth" select="$maxdepth"/>
				</xsl:call-template>
			</xsl:for-each>
		</xsl:if>
	</xsl:template>
</xsl:stylesheet>